plugins {
    id 'java'
    id 'application'
}

group = 'pl.kontomatik'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

dependencies {
    compile 'org.jsoup:jsoup:1.13.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.11.4'
    testCompile 'org.mockito:mockito-inline:3.7.0'
    testCompile 'org.mockito:mockito-junit-jupiter:3.7.0'
    testCompile 'org.junit.jupiter:junit-jupiter:5.6.3'
    testCompile 'org.mock-server:mockserver-junit-jupiter:5.11.2'
}

test {
    useJUnitPlatform()
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    useJUnitPlatform()

    reports {
        html.enabled true
        junitXml.enabled true
    }
}

application {
    mainClass = 'pl.kontomatik.challenge.ChallengeApplication'
}

run {
    standardInput = System.in
}
